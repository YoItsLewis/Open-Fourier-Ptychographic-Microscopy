/* 
 *  Acquire a live FFT from a screen region
 *  
 *  Nicol√°s De Francesco, Nov 2019 - www.image.sc user: @NicoDF
 * 
 *  Use:  - install the macro set
 *        - F1 key: setup the location of the screen from which the FFT is calcultated FFT.
 *        - F2 key: start/stop the live FFT feed. It will stop if the window is closed.
 *        - F5 key: modifiy the FFT refresh rate.
 *
 *  The macro will remember the location of the live window if it is closed.
 *
 */

var bx=50; // default acqusition window parameters
var by=50;
var bw=256;
var bh=256;

var time_step=200; // milliseconds

var FFT_name="Live FFT";

var FFT_x=820;
var FFT_y=450;
var FFT_w=256;
var FFT_h=256;

var FFT_running=false;

macro "Select target area [F1]" {
	FFT_running=false;
	run("Capture Screen");
	setTool("rectangle");
	makeRectangle(bx,by,bw,bh);
	waitForUser("Select capture area");
	
	if (isOpen("Screenshot")) {
   		selectWindow("Screenshot");
		getSelectionBounds(bx,by,bw,bh);
		close();
   		}
   		
	if (isOpen(FFT_name)) {
		close(FFT_name);
		run("Capture single FFT");
		}
	}

macro "Start/stop Live FFT [F2]" {
	FFT_running = !FFT_running;
	
	while (FFT_running)	{	
		run("Capture single FFT");
		setOption("Changes", false); // allows closing without prompt
		showStatus("LIVE FFT");
		wait(time_step);
		if(!isOpen(FFT_name)) FFT_running=false;
		}
		
	showStatus("FFT STOPPED");	
	}

macro "Set time step [F5]"{
	// setup
	Dialog.create("Capture Settings");
	Dialog.addNumber("Time Step (ms)", time_step);
	Dialog.show();
	time_step=Dialog.getNumber();
	}

macro "Capture single FFT" {
	setBatchMode(true);
	run("Capture Screen");
	makeRectangle(bx,by,bw,bh);
	run("FFT");
   	run("Copy");
	FFT_w=getWidth();
	FFT_h=getHeight();
   	close(); //close temp FFT
   	close(); //close screenshot

   	if (isOpen(FFT_name)) { 
   		selectWindow(FFT_name);
		getLocationAndSize(FFT_x, FFT_y, temp_w, temp_h);
   		} 
   	
   	else {
   		newImage(FFT_name, "8-bit black", FFT_w, FFT_h, 1);
   		}
	  		
   	run("Paste");
	run("Select None");
	setBatchMode("exit and display");
	selectWindow(FFT_name);
	setLocation(FFT_x, FFT_y);
   	}	
